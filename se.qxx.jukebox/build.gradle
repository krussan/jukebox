plugins {
   id 'java'
   id 'idea'
}

group = 'se.qxx.jukebox'
version = "${jukeboxVersion}"

ext {
   def outputDirname = base.distsDirectory.get()
   outputDir = new File("$outputDirname")
}

tasks.register('myCopyTask', Copy) {
   if (!outputDir.exists())
      outputDir.mkdirs()

   from(projectDir) {
      include 'settings.yaml'
      include 'imdb.yaml'
      include 'parser.yaml'
      include 'logging.properties'
   }
   into outputDir
}

java {
   sourceCompatibility = JavaVersion.VERSION_17
   targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
   sourceCompatibility = 17
   targetCompatibility = 17

   dependsOn myCopyTask
}

sourceSets {
   main {
      java {
         srcDirs = ['src/main/java', 'build/generated/sources']
      }
      resources {
         srcDirs = ['src/main/resources', 'src/main/native']
      }
   }
   test {
      java {
         srcDirs = ['src/test/java', 'build/generated/sources']
      }
      resources {
         srcDirs = ['src/test/resources']
      }
   }
}

jar {
   manifest {
      attributes(
         'Main-Class': 'se.qxx.jukebox.Jukebox'
      )
   }
}

tasks.register('fatJar', Jar) {
   manifest.from jar.manifest
   destinationDirectory = outputDir
   archiveClassifier = 'all'
   duplicatesStrategy = DuplicatesStrategy.EXCLUDE
   archiveBaseName = 'jukebox'

   from {
      configurations.runtimeClasspath
              .filter({ !(it.name =~ /sisu-guice.*\.jar/ || it.name =~ /git-commit-id.*\.jar/) })
              .collect { it.isDirectory() ? it : zipTree(it) }
   } {
      exclude "META-INF/*.SF"
      exclude "META-INF/*.DSA"
      exclude "META-INF/*.RSA"
   }
   with jar
}

dependencies {
   implementation project(':se.qxx.jukebox.domain')
   implementation 'com.google.protobuf:protobuf-java:3.21.12'
   implementation 'org.apache.commons:commons-lang3:3.12.0'
   implementation 'se.qxx.protodb:protodb-java:2.5.4'
   implementation 'commons-io:commons-io:2.11.0'
   implementation 'uk.co.caprica:vlcj:3.10.1'
   implementation 'org.imgscalr:imgscalr-lib:4.2'
   implementation "io.grpc:grpc-stub:${grpcVersion}"
   implementation 'org.xerial:sqlite-jdbc:3.41.2.2'
   implementation 'mysql:mysql-connector-java:5.1.45'
   implementation 'org.nanohttpd:nanohttpd-webserver:2.3.1'
   implementation 'fr.noop:subtitle:0.9.6'
   implementation 'org.freemarker:freemarker:2.3.31'
   implementation 'org.jsoup:jsoup:1.15.3'
//   implementation 'pl.project13.maven:git-commit-id-plugin:2.2.2'
   implementation 'com.matthewn4444:embl-java:1.0.0'
   implementation 'net.bramp.ffmpeg:ffmpeg:0.7.0'
   implementation 'net.java.dev.jna:jna:5.12.1'
   implementation 'com.google.inject:guice:5.1.0'
   implementation 'com.google.inject.extensions:guice-assistedinject:5.1.0'
   implementation 'com.google.guava:guava:33.1.0-jre'
   implementation 'org.apache.commons:commons-text:1.10.0'
   implementation "javax.annotation:javax.annotation-api:1.3.2"
   implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.2'
   implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'

   // should be added by protodb dependency but is missing
   implementation 'commons-codec:commons-codec:1.15'


   // runtime
   implementation 'com.sun.activation:jakarta.activation:2.0.1'

   // grpc
   implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
   implementation "io.grpc:grpc-protobuf:${grpcVersion}"
   implementation "io.grpc:grpc-stub:${grpcVersion}"

   // test
   testImplementation 'junit:junit:4.13.1'
   testImplementation 'org.mockito:mockito-core:2.20.0'
}

artifacts {
   archives fatJar
}

tasks.register('createScripts') {
   doLast {
      def jarFile = fatJar.archiveFileName.get()
      new File(outputDir, "run.sh").text = """#!/bin/sh
if [ -n "\$LD_LIBRARY_PATH" ]
then
   LD_LIBRARY_PATH=\$PWD:\${LD_LIBRARY_PATH}
else
   LD_LIBRARY_PATH=+\$PWD
fi
export LD_LIBRARY_PATH=\$LD_LIBRARY_PATH
java -Xmx1024m -Djava.util.logging.config.file=logging.properties -jar $jarFile \${1+"\$@"} &
echo \$! > juebox.pid
"""
   }
}

tasks.register('archiveZip', Zip) {
   dependsOn fatJar, createScripts

   from outputDir
   include '*.jar'
   include '*.xml'
   include 'logging.properties'
   include '*.sh'

   archiveBaseName = 'jukebox'
   destinationDirectory = outputDir
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}