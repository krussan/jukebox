plugins {
    id 'com.android.application'
    id 'com.github.triplet.play'
    id 'idea'
}

def appVersionName = version ?: '1.0'
def appVersionCode = Integer.valueOf(System.env.TRAVIS_BUILD_NUMBER ?: project.findProperty('versionCode') ?: 1)
def appJsonFile = project.findProperty('apiJsonFile') ?: '../ci/api.json'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "se.qxx.android.jukebox"
        minSdkVersion 26
        targetSdkVersion 28

        // Enabling multidex support.
        multiDexEnabled true
        versionCode appVersionCode
        versionName appVersionName
        setProperty("archivesBaseName", "jukebox-$versionName")

    }

    signingConfigs {
        release {
            if (System.env['RELEASE_STORE_FILE'] != null)
                storeFile file(System.env['RELEASE_STORE_FILE'])

            if (System.env["RELEASE_STORE_PASSWORD"] != null)
                storePassword System.env["RELEASE_STORE_PASSWORD"]

            if (System.env["RELEASE_KEY_ALIAS"] != null)
                keyAlias System.env["RELEASE_KEY_ALIAS"]

            if (System.env["RELEASE_KEY_PASSWORD"] != null)
                keyPassword System.env["RELEASE_KEY_PASSWORD"]

            if (project.hasProperty("RELEASE_STORE_FILE"))
                storeFile file(RELEASE_STORE_FILE)

            if (project.hasProperty("RELEASE_STORE_PASSWORD"))
                storePassword RELEASE_STORE_PASSWORD

            if (project.hasProperty("RELEASE_KEY_ALIAS"))
                keyAlias RELEASE_KEY_ALIAS

            if (project.hasProperty("RELEASE_KEY_PASSWORD"))
                keyPassword RELEASE_KEY_PASSWORD

        }
    }

     buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.txt'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }

    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

play {
    track = "internal"
    serviceAccountCredentials = file(appJsonFile)
}

dependencies {
    implementation project(':se.qxx.jukebox.domain')
    implementation project(':se.qxx.jukebox.comm')
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.google.protobuf:protobuf-java:3.9.0'
    implementation 'se.qxx.android.tools:android-tools:1.0.2'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.google.android.gms:play-services-cast-framework:17.1.0'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'com.android.support:mediarouter-v7:28.0.0'
}
