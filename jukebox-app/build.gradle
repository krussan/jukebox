plugins {
    id 'com.android.application'
    id 'com.github.triplet.play'
    id 'idea'
}

def appVersionName = "${jukeboxVersion}"
def appVersionCode = Integer.valueOf(System.env.TRAVIS_BUILD_NUMBER ?: project.findProperty('versionCode') as String ?: 1 as String)
def appJsonFile = project.findProperty('apiJsonFile') ?: '../ci/api.json'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "se.qxx.android.jukebox"
        minSdkVersion 26
        targetSdkVersion 33

        // Enabling multidex support.
        multiDexEnabled true
        versionCode appVersionCode
        versionName appVersionName
        setProperty("archivesBaseName", "jukebox-$versionName")

    }

    signingConfigs {
        release {
            if (System.env['RELEASE_STORE_FILE'] != null)
                storeFile file(System.env['RELEASE_STORE_FILE'])

            if (System.env["RELEASE_STORE_PASSWORD"] != null)
                storePassword System.env["RELEASE_STORE_PASSWORD"]

            if (System.env["RELEASE_KEY_ALIAS"] != null)
                keyAlias System.env["RELEASE_KEY_ALIAS"]

            if (System.env["RELEASE_KEY_PASSWORD"] != null)
                keyPassword System.env["RELEASE_KEY_PASSWORD"]

            if (project.hasProperty("RELEASE_STORE_FILE"))
                storeFile file(RELEASE_STORE_FILE)

            if (project.hasProperty("RELEASE_STORE_PASSWORD"))
                storePassword RELEASE_STORE_PASSWORD

            if (project.hasProperty("RELEASE_KEY_ALIAS"))
                keyAlias RELEASE_KEY_ALIAS

            if (project.hasProperty("RELEASE_KEY_PASSWORD"))
                keyPassword RELEASE_KEY_PASSWORD

        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.txt'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }

    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations {
        implementation.exclude module: 'protobuf-lite'
    }
}

play {
    track = "internal"
    serviceAccountCredentials = file(appJsonFile)
}

dependencies {
    implementation project(':se.qxx.jukebox.domain')
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.google.protobuf:protobuf-java:3.12.0'
    implementation 'se.qxx.android.tools:android-tools:1.0.2'
    implementation 'com.google.android.gms:play-services-cast-framework:21.0.1'

    implementation 'org.apache.commons:commons-collections4:4.1'

    implementation "androidx.core:core:1.8.0"
    implementation "androidx.annotation:annotation:1.4.0"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "androidx.mediarouter:mediarouter:1.3.0"
    implementation "androidx.preference:preference:1.2.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation 'com.google.android.material:material:1.6.1'

    implementation "io.grpc:grpc-okhttp:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    // test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.20.0'

}
